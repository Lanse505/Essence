buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven {url "https://plugins.gradle.org/m2/"}
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "com.wynprice.cursemaven:CurseMaven:2.1.1"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: "java"
apply plugin: "idea"
apply plugin: "com.wynprice.cursemaven"
apply plugin: "maven"

version = "${minecraft_version}-" + getVersion()
group = "${group}"
archivesBaseName = "${archive}"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        name = "Titanium"
        url  = 'https://cdn.hrzn.studio/maven/'
    }
    maven {
        name = "JEI"
        url  = 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        name = "Curios"
        url  = 'https://maven.theillusivec4.top/'
    }
    maven {
        name = "Patchouli"
        url  = 'https://maven.blamejared.com'
    }
    maven {
        name = "Registrate"
        url  = "http://maven.tterrag.com/"
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    main {
        resources {
            include '**/**'
            srcDirs += "$rootDir/src/generated/resources"
            exclude '.cache'
        }
    }
}

def version_properties = [
        "version"       : version,
        "mc_version"    : minecraft_version,
        "forge_version" : forge_version,
        "loader_version": loader_version
]


processResources {
    outputs.upToDateWhen { false }
    from(sourceSets.main.resources) {
        include "META-INF/mods.toml"
        expand version_properties
    }
    into "$buildDir/resources/main/"
}

minecraft {
    mappings channel: "snapshot", version: "${mappings_version}"
    runs {
        client {
            workingDirectory project.file('run/client')
            properties 'fml.earlyprogresswindow': 'false'
            properties 'mixin.env.disableRefMap': 'true'
            mods {
                essence {
                    source(sourceSets.main)
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            properties 'fml.earlyprogresswindow': 'false'
            properties 'mixin.env.disableRefMap': 'true'
            mods {
                essence {
                    source(sourceSets.main)
                }
            }
        }

        data {
            workingDirectory project.file('run/data')
            properties 'fml.earlyprogresswindow': 'false'
            properties 'mixin.env.disableRefMap': 'true'
            args '--all', '--output', file('src/generated/resources/'),
                    '--reports',
                    '--mod', 'essence',  '--existing', file('src/main/resources')
            mods {
                essence {
                    source(sourceSets.main)
                }
            }
        }
    }
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

configurations {
    shade
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', "com.teamacronymcoders.essence.repack.registrate"
    classifier ''
}

dependencies {
    // Forge
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Titanium
    compile fg.deobf(project.dependencies.create("com.hrznstudio:titanium:${titanium_version}") {
        transitive = false
    })

    // Registrate
    def registrate = "com.tterrag.registrate:Registrate:MC1.16.2-${registrate_version}"
    implementation fg.deobf(registrate)
    shade registrate

    //Patchouli
    compile fg.deobf("vazkii.patchouli:Patchouli:${old_minecraft_version}-${patchouli_version}")

    // JEI
    compileOnly fg.deobf("mezz.jei:jei-${old_minecraft_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${old_minecraft_version}:${jei_version}")

    // Curios
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${old_minecraft_version}-${curios_version}:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${old_minecraft_version}-${curios_version}")
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

task registrateJar(type: Jar, dependsOn: javadoc) {
    classifier = "registrate"
    from sourceSets.main.allSource
    exclude '**/**'
    include 'com/teamacronymcoders/essence/registrate/**'
}

jar {
    classifier = "nodep"
    manifest {
        attributes([
                "Specification-Title"     : "${title}",
                "Specification-Vendor"    : "${team}",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "${team}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "ModSide": "BOTH",
        ],)
    }
}

reobf {
    shadowJar{}
    registrateJar{}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar
build.dependsOn registrateJar
build.dependsOn reobfRegistrateJar

artifacts {
    archives javadocJar
    archives srcJar
    archives shadowJar
    archives registrateJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
        }
    }
}

String getVersion() {
    String version = "${mod_version}"
    String branch = project.hasProperty("branch") ? project.branch : ""

    if (branch != "") {
        version += "-" + branch

        String buildNumber = getBuildNumber()
        if (buildNumber != "") {
            version += "." + buildNumber
        }
    }

    return version
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if (buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? System.getenv("bamboo_buildNumber") : ""
    }
    return buildNumber
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "10000"
    }
}